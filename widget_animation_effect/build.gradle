apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: realmPlug
android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "ll.leon.com.widget_animation_effect"
        minSdkVersion minVer
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexKeepFile file ('multidex.keep')
        vectorDrawables.useSupportLibrary = true

    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode false
                removeUnusedResources false
                obfuscate false
                optimizeCode false
                proguardFile 'proguard-rules.pro'
            }
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '26.0.2'
                }
            }
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries = false
        //是否支持大工程模式
        jumboMode = true
        //预编译
        // preDexLibraries = true
        //线程数
        threadCount = 8
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':baselibs')
    implementation 'me.leon406:picture_library:1.2.4'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:support-vector-drawable:27.0.2'
    implementation 'com.github.xujiaji:happy-bubble:1.1.0'
    implementation 'com.github.lzyzsd:jsbridge:1.0.4'
    annotationProcessor knifeAPT
    //    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.kingja.loadsir:loadsir:1.2.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.android.support.test:runner:1.0.1'
    // /
    implementation 'org.greenrobot:greendao:3.2.0'
    implementation project(':zxinglib')
    implementation project(':bankcardrecognize')
}
greendao {
    schemaVersion 1//数据库版本号
    daoPackage 'll.leon.com.widget_animation_effect.bean.greendao'//设置DaoMaster、DaoSession、Dao包名
    targetGenDir 'src/main/java'//设置DaoMaster、DaoSession、Dao目录
    //targetGenDirTest：设置生成单元测试目录
    //generateTests：设置自动生成单元测试用例
}
afterEvaluate {
    tasks.matching{
        it.name.startsWith('create') && it.name.endsWith('MainDexClassList')
    }.each { tk ->
        tk.doLast {
            keepMainMultiDex(tk.outputFile)
        }
    }
}

/**
 * 控制MainDex中的class列表
 * 将multidex.keep的内容追加到 maindexlist.txt 中
 * @param outputFile
 */
def keepMainMultiDex(File outputFile){
    File keepFile = file("multidex.keep")
    outputFile << '\n'
    outputFile << keepFile.getText('UTF-8')
}

